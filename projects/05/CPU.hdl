// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
        // Se tiene que ver si la instruccion si es A.
        // Viendo el primer bit y el bit A de ADM
        Not(in=instruction[15], out=loadInstruction);
        Or(a=instruction[5],    b=loadInstruction, out=loadA);

        Mux16(a=ALUout,    b=instruction, sel=loadInstruction, out=mux1);
        ARegister(in=mux1, load=loadA,    out=areg,            out[0..14]=addressM);
        Mux16(a=areg, b=inM, sel=instruction[12], out=inputY);

        And(a=instruction[15], b=instruction[4], out=loadD);
        DRegister(in=ALUout, load=loadD, out=inputX);
        
        // cccccc LINE
        ALU(x  = inputX,
            y  = inputY, 
            zx = instruction[11], 
            nx = instruction[10], 
            zy = instruction[9], 
            ny = instruction[8],
            f  = instruction[7], 
            no = instruction[6], 
            out= ALUout, 
            out= outM, 
            zr = zero, 
            ng = negative);

        // ddd LINE
        // instruction[5] --> A
        // instruction[4] --> D
        // instruction[3] --> M

        And(a=instruction[3], b=instruction[15], out=writeM);

        // jjj LINE
        // instruction[2] --> < Menor
        // instruction[1] --> = Igual
        // instruction[0] --> > Mayor

        Not(in=negative, out=morezero);
        Not(in=zero,     out=notzero);

        // Estrictamente positivo
        And(a=notzero, b=morezero, out=postive);

        And(a=instruction[2], b=negative, out=itslesszero);
        And(a=instruction[0], b=postive,  out=itsmorezero);
        And(a=instruction[1], b=zero,     out=itszero);

        Or(a=itslesszero, b=itsmorezero, out=orJmp);
        Or(a=orJmp, b=itszero, out=itsJump);

        And(a=itsJump, b=instruction[15], out=loadPC);
        Not(in=loadPC, out=incPC);

        PC(in=areg, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc);        
}